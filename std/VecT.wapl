use "./std/Iterator.wapl"

struct VecT{
    ptr:T data,
    i64 elem_size,
    i64 len,
    i64 cap,
}

fn VecT_new(i64 e_size,i64 capa):VecT{
    #=(v,_,VecT);
    #=(vp,&_(v),ptr:VecT);
    #=(d,malloc(*(e_size,capa),T),ptr:T);
    =(val(->(vp,data)),d);
    =(val(->(vp,elem_size)),e_size);
    =(val(->(vp,len)),0);
    =(val(->(vp,cap)),capa);
    return v;
}

fn VecT_push(ptr:VecT &self,ptr:T value):ptr:T{
    warptoif(==(_>(&self,cap),_>(&self,len)),capaover,push);
    point capaover;
    =(val(->(&self,data)),realloc(_>(&self,data),*(_>(&self,elem_size),*(+(_>(&self,cap),1),2)),T));
    =(val(->(&self,cap)),*(+(_>(&self,cap),1),2));
    warpto(push);
    point push;
    memcpy(ptr_add(_>(&self,data),*(_>(&self,len),_>(&self,elem_size))),value,_>(&self,elem_size));
    =(val(->(&self,len)),+(_>(&self,len),1));
    return value;
}

fn VecT_get(VecT self,i64 indx):ptr:T{
    return ptr_add(.(self,data),*(indx,.(self,elem_size)));
}

fn VecT_free(ptr:VecT &self){
    free(_>(&self,data));
    =(val(->(&self,len)),0);
    =(val(->(&self,cap)),0);
}

fn VecT_from(&:T srcData,i64 e_size, i64 size):VecT{
    #=(v,_,VecT);
    #=(vp,&_(v),ptr:VecT);
    #=(d,malloc(*(e_size,size),T),*:T);
    memcpy(d,srcData,*(e_size,size));
    =(val(->(vp,data)),pmove(d));
    =(val(->(vp,elem_size)),e_size);
    =(val(->(vp,len)),size);
    =(val(->(vp,cap)),size);
    return v;    
}

fn VecT_connect(ptr:VecT &self,ptr:VecT value):ptr:VecT{
    #=(newlen,+(_>(&self,len),_>(value,len)),i64);
    #=(needed, *(newlen, _>(&self,elem_size)),i64);
    loopif:capaover(<=(_>(&self,cap),newlen)){
        #=(newcap,*( newlen, 2 ),i64);
        =(_>(&self,data),realloc(_>(&self,data), *(newcap,_>(&self,elem_size)),T));
        =(_>(&self,cap),newcap);
        warpto(break-capaover);
    }
    memcpy(ptr_add(_>(&self,data),*(_>(&self,len),_>(&self,elem_size))),_>(value,data),*(_>(value,len),_>(value,elem_size)));
    =(_>(&self,len),newlen);
    return value;
}

fn VecT_rangei64(i64 start,i64 end,i64 step):VecT{
    #=(v,VecT_new(8,/(-(end,start),step)),VecT);
    #=(i,start,i64);
    loopif:(<(i,end)){
        VecT_push(&_(v),&_(i));
        =(i,+(i,step));
    }
    return v;
}

fn VecT_iter(VecT vec):Iterator{
    return iter(.(vec,data),VecT_get(vec,.(vec,len)),.(vec,elem_size));
}